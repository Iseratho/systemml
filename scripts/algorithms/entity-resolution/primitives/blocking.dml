#-------------------------------------------------------------
#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
#-------------------------------------------------------------

# Splits the rows of X into num_blocks non-overlapping regions.
# May produce less than num_blocks regions.
#
# INPUT PARAMETERS:
# --------------------------------------------------------------------------------------------
# NAME          TYPE    DEFAULT   MEANING
# --------------------------------------------------------------------------------------------
# X             matrix  ---       A dataset with rows to split into blocks.
# num_blocks    Integer ---       How many blocks to produce.
#
# Output:
# --------------------------------------------------------------------------------------------
# NAME          TYPE     MEANING
# --------------------------------------------------------------------------------------------
# BLOCKS         Double   A column vector with start indices for each block. Has one more rows
#                         than blocks such that the last row is the end index of the last block.
# --------------------------------------------------------------------------------------------
naive = function(Matrix[Double] X, Integer num_blocks) return (Matrix[Double] BLOCKS) {
  block_size_flt = nrow(X) / num_blocks;
  if (block_size_flt < 1.0) {
    BLOCKS= seq(1, nrow(X) + 1);
  } else {
    block_size = ceil(block_size_flt);
    BLOCKS = rbind(as.matrix(1), 1 +  block_size * seq(1, num_blocks));
    BLOCKS[num_blocks+1,] = nrow(X) + 1
  }
}

# Sorts the rows in dataset X by the vector v. The order is defined by the ascending order of v.
# Can return the vector v as first column of X, depending on parameter prepend_v.
#
# INPUT PARAMETERS:
# --------------------------------------------------------------------------------------------
# NAME          TYPE    DEFAULT   MEANING
# --------------------------------------------------------------------------------------------
# X             matrix  ---       Any matrix with rows to be sorted.
# v             matrix  ---       A vector with the same number of rows as X. Defines ordering.
# prepend_v     boolean ---       Whether to return v as first column of X.
#
# Output:
# --------------------------------------------------------------------------------------------
# NAME          TYPE     MEANING
# --------------------------------------------------------------------------------------------
# X_index       matrix  A vector with the original row number for each row in X.
# X_sorted      matrix  The sorted input matrix X.
# --------------------------------------------------------------------------------------------
sort_by_vector = function(Matrix[Double] X, Matrix[Double] v, Boolean prepend_v) return (Matrix[Double] X_index, Matrix[Double] X_sorted) {
  X_index = order(target=v, by=1, decreasing = FALSE, index.return=TRUE);
  X_sorted = order(target=cbind(v, X), by=1, decreasing = FALSE, index.return=FALSE);
  if (!prepend_v) {
    X_sorted = X_sorted[,2:ncol(X_sorted)];
  }
}

# Sorts the rows in dataset X by their sum.
#
# INPUT PARAMETERS:
# --------------------------------------------------------------------------------------------
# NAME          TYPE    DEFAULT   MEANING
# --------------------------------------------------------------------------------------------
# X             matrix  ---       Any matrix with rows to be sorted.
#
# Output:
# --------------------------------------------------------------------------------------------
# NAME          TYPE     MEANING
# --------------------------------------------------------------------------------------------
# X_index       matrix  A vector with the original row number for each row in X.
# X_sorted      matrix  The sorted input matrix X.
# --------------------------------------------------------------------------------------------
row_sum_sorting = function(Matrix[Double] X) return (Matrix[Double] X_index, Matrix[Double] X_sorted) {
  X_rowSum = rowSums(X);
  [X_index, X_sorted] = sort_by_vector(X, X_rowSum, FALSE);
}

# Sorts the rows in dataset X by their original row numbers (index).
#
# INPUT PARAMETERS:
# --------------------------------------------------------------------------------------------
# NAME          TYPE    DEFAULT   MEANING
# --------------------------------------------------------------------------------------------
# X             matrix  ---       Any matrix with rows to be sorted.
# X_index       matrix  ---       The orginal row numbers to be restored.
#
# Output:
# --------------------------------------------------------------------------------------------
# NAME          TYPE     MEANING
# --------------------------------------------------------------------------------------------
# X_reindex     matrix  The reindexed matrix X.
# --------------------------------------------------------------------------------------------
reindex_rowwise = function(Matrix[Double] X, Matrix[Double] X_index) return (Matrix[Double] X_reindex) {
  X_conc = cbind(X_index, X);
  X_reindex = order(target=X_conc, by=1, decreasing = FALSE, index.return=FALSE);
  # Remove index column
  X_reindex = X_reindex[,2:ncol(X_reindex)];
}

reindex_rows_and_cols = function(Matrix[Double] X, Matrix[Double] X_index) return (Matrix[Double] X_reindex) {
  # First reindex rows
  X_reindex = X;
  X_reindex = reindex_rowwise(X_reindex, X_index);
  # Then transpose and repeat
  X_reindex = t(X_reindex);
  X_reindex = reindex_rowwise(X_reindex, X_index);
}

hash_blocking = function(Matrix[Double] X, Integer num_blocks) return (Matrix[Double] BLOCKS) {
  BLOCKS = X %% num_blocks;
}

# split_hash_2 = function(Matrix[Double] X) return (List[Unknown] BLOCKLIST) {
#   BLOCKS = X %% 2;
#   BLOCKLIST = list(BLOCKS == 1, BLOCKS == 0);
# }

gen_rand_hyperplanes = function(Integer num_hyperplanes, Integer dimension) return (Matrix[Double] H) {
  H = rand(rows=num_hyperplanes, cols=dimension, min=-1, max=1);
}

lsh = function(Matrix[Double] X, Integer num_hyperplanes) return (Matrix[Double] X_code) {
  H = gen_rand_hyperplanes(num_hyperplanes, ncol(X));
  X_hash = X %*% t(H);
  X_code = (X_hash > 0) - (X_hash < 0);
}

aggregate_lsh_code = function(Matrix[Double] X_code) return (Matrix[Double] X_agg) {
  X_pos = (X_code > 0);
  pos_weights = 2^seq(ncol(X_code)-1, 0);
  X_weights = X_pos * t(pos_weights);
  X_agg = rowSums(X_weights);
}

lsh_sorting = function(Matrix[Double] X, Integer num_hyperplanes) return (Matrix[Double] X_index, Matrix[Double] X_hash, Matrix[Double] X_sorted) {
  X_code = lsh(X, num_hyperplanes);
  X_agg = aggregate_lsh_code(X_code);
  [X_index, X_sorted_with_hash] = sort_by_vector(X, X_agg, TRUE);
  X_sorted = X_sorted_with_hash[,2:ncol(X_sorted_with_hash)];
  X_hash = X_sorted_with_hash[,1];
}
